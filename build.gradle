plugins {
	id 'net.researchgate.release' version '3.1.0'
	id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin' version '2.0.7'
	id 'com.diffplug.spotless' version '8.2.1'
}

subprojects {
	group = 'uk.gov.laa.ccms.caab'
}

release {
	tagTemplate = '$name-$version'
}

repositories {

	// Latest version of saml requires this
	maven { url = 'https://build.shibboleth.net/nexus/content/repositories/releases/' }

	// Define the common configuration block for GitHub repositories
	def githubRepoConfig = { repoUrl ->
		maven {
			url = repoUrl
			credentials {
				username = System.getenv("GITHUB_ACTOR")?.trim() ?: project.findProperty('project.ext.gitPackageUser')
				password = System.getenv("GITHUB_TOKEN")?.trim() ?: project.findProperty('project.ext.gitPackageKey')
			}
		}
	}

	// Configure repositories using the defined GitHub config
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-data-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-caab-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-soa-gateway-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-caab-assessment-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-spring-boot-common')
}

dependencies {

	/**
	 * Vulnerabilities - Transitive dependency overrides (remove when bumped in parent library)
	 */
	constraints {
// 		// Example
//		implementation('group:name:version') {
//			because('Fixes CVE-XXXX-XXXXX Parent: parent-group:parent-name')
//		}

		implementation('org.bouncycastle:bcprov-jdk18on:1.83') {
			because('Fixes multiple CVEs Parent: org.opensaml:opensaml-saml-api')
		}

		implementation('org.bouncycastle:bcpkix-jdk18on:1.83') {
			because('Fixes multiple CVEs Parent: org.opensaml:opensaml-saml-api')
		}

		implementation('commons-io:commons-io:2.21.0') {
			because('Fixes CVE-2024-47554')
		}

		implementation "org.opensaml:opensaml-saml-api:5.1.3"
		implementation "org.opensaml:opensaml-saml-impl:5.1.3"
	}

	// Fixes CVE-2025-48924 - remove this when bumped in parent library
    implementation 'org.apache.commons:commons-lang3:3.18.0'

	implementation 'uk.gov.laa.ccms.data:data-api:0.0.61'

	implementation 'uk.gov.laa.ccms.soa.gateway:soa-gateway-api:0.0.70'

	implementation 'uk.gov.laa.ccms.caab:caab-api:0.0.61'

	implementation 'uk.gov.laa.ccms.caab:assessment-api:0.0.38'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.mapstruct:mapstruct:1.6.3'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

	// Starters
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-micrometer-tracing-brave'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-aspectj'
	implementation 'org.springframework.data:spring-data-commons'

	// AWS S3 Integration
	implementation platform('io.awspring.cloud:spring-cloud-aws-dependencies:4.0.0')
	implementation 'io.awspring.cloud:spring-cloud-aws-starter'
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'
	implementation 'software.amazon.awssdk:sts:2.41.27'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.json:json:20250517'

	implementation 'io.swagger.core.v3:swagger-annotations:2.2.42'

	//SAML security
	implementation 'org.springframework.boot:spring-boot-starter-security-saml2'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'org.springframework.security:spring-security-saml2-service-provider'

	implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-govuk-dialect'

	// Metrics
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.prometheus:prometheus-metrics-tracer-initializer'

	//ClamAV virus scan
	implementation 'fi.solita.clamav:clamav-client:1.0.1'

	// Front end
	// https://mvnrepository.com/artifact/org.webjars/webjars-locator-lite
	implementation 'org.webjars:webjars-locator-lite'
	// https://mvnrepository.com/artifact/org.webjars.npm/govuk-frontend
	implementation 'org.webjars.npm:govuk-frontend:5.11.0'
	implementation 'org.webjars.npm:ministryofjustice__frontend:5.1.3'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'io.projectreactor:reactor-test:3.8.3'

	integrationTestImplementation 'org.wiremock:wiremock-standalone:3.13.1'

	testImplementation platform('org.testcontainers:testcontainers-bom:1.21.3')
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:localstack:1.21.3'

}

test {
	useJUnitPlatform()

	// Hide warning for dynamic loading of agents https://github.com/mockito/mockito/issues/3037
	jvmArgs '-XX:+EnableDynamicAgentLoading'

	finalizedBy jacocoTestReport
}

jacocoTestReport {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/CaabApplication.class', '**/SecurityConfiguration.class'])
		}))
	}
}

jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/CaabApplication.class', '**/SecurityConfiguration.class'])
		}))
	}
}

spotless {
	java {
		target 'src/*/java/**/*.java'
		importOrder()
		removeUnusedImports()
		cleanthat()
		googleJavaFormat()
	}
}