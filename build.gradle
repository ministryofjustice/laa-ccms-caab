plugins {
	id 'net.researchgate.release' version '3.1.0'
	id 'uk.gov.laa.ccms.springboot.laa-ccms-spring-boot-gradle-plugin' version '0.0.32'
}

subprojects {
	group = 'uk.gov.laa.ccms.caab'
}

release {
	tagTemplate = '$name-$version'
}

repositories {

	// Latest version of saml requires this
	maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" }

	// Define the common configuration block for GitHub repositories
	def githubRepoConfig = { repoUrl ->
		maven {
			url repoUrl
			credentials {
				username = System.getenv("GITHUB_ACTOR")?.trim() ?: project.findProperty('project.ext.gitPackageUser')
				password = System.getenv("GITHUB_TOKEN")?.trim() ?: project.findProperty('project.ext.gitPackageKey')
			}
		}
	}

	// Configure repositories using the defined GitHub config
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-data-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-caab-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-soa-gateway-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-caab-assessment-api')
	githubRepoConfig('https://maven.pkg.github.com/ministryofjustice/laa-ccms-spring-boot-common')
}

dependencies {

	/**
	 * Vulnerabilities - Transitive dependency overrides (remove when bumped in parent library)
	 */
	constraints {
		// Production dependencies
		implementation('org.bouncycastle:bcprov-jdk18on:1.80') {
			because("Fixes CVE-2024-30172, CVE-2024-30171, CVE-2023-33202," +
					"CVE-2024-29857, CVE-2023-33201." +
					"Parent: org.springframework.security:spring-security-saml2-service-provider")
		}
		implementation('com.google.guava:guava:32.0.0-jre') {
			because("Fixes CVE-2023-2976." +
					"Parent: org.springframework.security:spring-security-saml2-service-provider")
		}

		// Test dependencies
		testImplementation('org.apache.commons:commons-compress:1.27.1') {
			because("Fixes CVE-2024-25710, CVE-2024-26308." +
					"Parent: org.testcontainers:testcontainers-bom")
		}

	}

	implementation 'uk.gov.laa.ccms.data:data-api:0.0.56'

	implementation 'uk.gov.laa.ccms.soa.gateway:soa-gateway-api:0.0.65'

	implementation 'uk.gov.laa.ccms.caab:caab-api:0.0.54'

	implementation 'uk.gov.laa.ccms.caab:assessment-api:0.0.38'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	// Starters
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.data:spring-data-commons'

	// AWS S3 Integration
	implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.1.1")
	implementation 'io.awspring.cloud:spring-cloud-aws-starter'
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'
	implementation 'software.amazon.awssdk:sts:2.26.19'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.json:json:20240303'

	implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'

	//SAML security
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'org.springframework.security:spring-security-saml2-service-provider'

	implementation 'uk.gov.laa.ccms.springboot:laa-ccms-spring-boot-starter-govuk-dialect'

	// Metrics
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'

	//ClamAV virus scan
	implementation 'fi.solita.clamav:clamav-client:1.0.1'

	// Front end
	// https://mvnrepository.com/artifact/org.webjars/webjars-locator-lite
	implementation 'org.webjars:webjars-locator-lite'
	// https://mvnrepository.com/artifact/org.webjars.npm/govuk-frontend
	implementation 'org.webjars.npm:govuk-frontend:5.7.1'
	implementation 'org.webjars.npm:ministryofjustice__frontend:3.3.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.projectreactor:reactor-test:3.6.1'

	integrationTestImplementation 'org.wiremock:wiremock-standalone:3.3.1'

	testImplementation platform('org.testcontainers:testcontainers-bom:1.20.6')
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:localstack:1.20.4'

}

test {
	useJUnitPlatform()

	// Hide warning for dynamic loading of agents https://github.com/mockito/mockito/issues/3037
	jvmArgs '-XX:+EnableDynamicAgentLoading'

	finalizedBy jacocoTestReport
}

jacocoTestReport {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/CaabApplication.class', '**/SecurityConfiguration.class'])
		}))
	}
}

jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/CaabApplication.class', '**/SecurityConfiguration.class'])
		}))
	}
}
